class Qsort
 {
    function void swap (Array a, int i, int j){
        var int temp;
        let temp = a[i];
        let a[i] = a[j];
        let a[j] = temp;
        return;
    }

    function int partition (Array a, int left, int right){
        var int pivot, i, j;
        let pivot = a[right];
        let i = left -1;
        let j = left;

        while (j<right){
            if (a[j]<pivot){
                let i = i +1;
                do Qsort.swap(a, i, j);
            }
            let j = j+1;
        }
        do Qsort.swap (a, i+1, right);
        return (i+1);
    }

    function void quicksort(Array a, int left, int right){
        var int pi;
        if (left < right){
            let pi = Qsort.partition(a, left, right);
            do Qsort.quicksort(a, left, pi-1);
            do Qsort.quicksort(a, pi + 1, right); 
        }
        return;
    }
 }